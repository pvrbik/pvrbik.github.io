PGCD:=proc(inA,inB,p,var)
local A,B,xk,C,c,lcA,lcB,g,q,H,n,b,gb,Ab,Bb,Cb,m,lcC,qs,Hs,Hold:
    A:=inA:
    B:=inB:
    
    xk:=var[-1]:
    
    if nops(var)=1 then
        C:=Gcd(A,B) mod p:
        if degree(C)=0 then
            C:=1:
        end if:
        return C:
    end if:
    
    A:=Primpart(A,var[1..-2],'a') mod p:
    B:=Primpart(B,var[1..-2],'b') mod p:
    c:=Gcd(a,b) mod p:
    
    lcA:=lcoeff(A,var[1..-2]):
    lcB:=lcoeff(B,var[1..-2]):
    g:=Gcd(lcA,lcB) mod p:
    
#   Main loop:
    qs:=NULL: Hs:=NULL: H:=0: Hold:=0:
    n:=min(degree(A), degree(B)):
    
    b:=0:
    while (true) do
        
        b:=b+1:
        
        gb:=eval(g,xk=b) mod p:
        while (gb=0) do
            b:=b+1:
            gb:=eval(g,xk=b) mod p: 
        end do:
        
        if (b>p) then error "FAIL": end if:
        
        Ab:=subs(xk=b,A) mod p:
        Bb:=subs(xk=b,B) mod p:
        
        Cb:=PGCD(Ab,Bb,p,var[1..-2]):
        m:=degree(Cb,var[1..-2]):
        
#       Normalize Cb so that lcoeff(Cb)=gb
        lcC:=lcoeff(Cb):
        Cb:=(gb/lcC mod p)*Cb mod p:
        
        if m<n then
            qs:=NULL: Hs:=NULL: H:=0:
            n:=m:
        elif m=n then
            qs:=qs,b:
            Hs:=Hs,Cb:
            if nops([qs])>degree(Ab,var[-2]) then
                Hold:=H:
                H:=Interp([qs],[Hs],xk) mod p:
            end if:
        end if:
        
        C:=Primpart(H,var[1..-2]) mod p:
        
        if Expand(Hold-H) mod p=0 and H<>0 then
            if Divide(A,C) mod p and Divide(B,C) mod p then
                return Primpart(c*C) mod p:
            elif m=0
                then return c:
            end if:
        end if:
        
    end do:
    
end proc: